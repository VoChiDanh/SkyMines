import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

java {
    sourceCompatibility(JavaVersion.VERSION_17)
    targetCompatibility(JavaVersion.VERSION_17)
}

group 'com.github.colingrime'
version '1.3.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://jitpack.io' }

    // NBTAPI
    maven { url 'https://repo.codemc.org/repository/maven-public/' }

    // FastAsyncWorldEdit
    maven { url 'https://mvn.intellectualsites.com/content/groups/public/' }
}

dependencies {
    // Spigot and Minecraft plugin dependencies
    implementation 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    implementation 'com.github.MilkBowl:VaultAPI:1.7'
    implementation 'de.tr7zw:item-nbt-api-plugin:2.10.0'
    implementation(platform('com.intellectualsites.bom:bom-1.18.x:1.19'))
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Core")
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit")

    // SQL stuff
    implementation 'com.zaxxer:HikariCP:5.0.1'

    // Annotations
    implementation 'org.jetbrains:annotations:23.0.0'
}

shadowJar {
    dependencies {
        include dependency('de.tr7zw:item-nbt-api-plugin:2.10.0')
        include dependency('com.zaxxer:HikariCP:5.0.1')
    }

    archiveFileName = "${project.name}-${project.version}.jar"
}

// Allows @version@ placeholder in plugin.yml
processResources {
    filter ReplaceTokens, tokens: ['version': version]
}